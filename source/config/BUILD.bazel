load("@rules_distroless//distroless:defs.bzl", "cacerts", "group", "home", "passwd")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_mkdirs", "pkg_mklink")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load(":defs.bzl", "directories")

[
    pkg_mklink(
        name = directory,
        link_name = directory,
        target = "usr/{}".format(directory),
    )
    for directory in directories
]

pkg_mkdirs(
    name = "tmp",
    attributes = pkg_attributes(
        group = "root",
        mode = "1777",
        user = "root",
    ),
    dirs = ["tmp"],
)

pkg_tar(
    name = "linux_standard_directories",
    srcs = [
        ":{}".format(directory)
        for directory in directories
    ] + [
        ":tmp",
    ],
    package_dir = "./",
)

cacerts(
    name = "cacerts",
    package = "@apt_packages//ca-certificates:data",
)

passwd(
    name = "passwd",
    entries = [
        dict(
            gid = 0,
            home = "/root",
            shell = "/usr/bin/bash",
            uid = 0,
            username = "root",
        ),
        dict(
            gid = 1000,
            home = "/home/nonroot",
            shell = "/usr/bin/bash",
            uid = 1000,
            username = "nonroot",
        ),
    ],
)

group(
    name = "group",
    entries = [
        dict(
            name = "root",
            gid = 0,
            users = ["root"],
        ),
        dict(
            name = "nonroot",
            gid = 1000,
            users = ["nonroot"],
        ),
    ],
)

home(
    name = "home",
    dirs = [
        dict(
            gid = 0,
            home = "/root",
            uid = 0,
        ),
        dict(
            gid = 1000,
            home = "/home/nonroot",
            uid = 1000,
        ),
    ],
)

pkg_tar(
    name = "linux_standard_config_files",
    srcs = [],
    deps = [
        ":cacerts",
        ":group",
        ":home",
        ":passwd",
    ],
)

pkg_tar(
    name = "config",
    srcs = [],
    compressor = "//tools/zstd",
    compressor_args = "--ultra -22",
    extension = "tar.zst",
    deps = [
        ":linux_standard_config_files",
        ":linux_standard_directories",
    ],
)
