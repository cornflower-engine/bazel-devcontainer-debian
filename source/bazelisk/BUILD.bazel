load("@rules_distroless//apt:defs.bzl", "dpkg_status")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

alias(
    name = "data",
    actual = select({
        "//platform/constraint:linux_amd64": "@bazelisk_amd64//:data",
        "//platform/constraint:linux_arm64": "@bazelisk_arm64//:data",
    }),
)

alias(
    name = "control",
    actual = select({
        "//platform/constraint:linux_amd64": "@bazelisk_amd64//:control",
        "//platform/constraint:linux_arm64": "@bazelisk_arm64//:control",
    }),
)

dpkg_status(
    name = "dpkg_status",
    controls = [":control"],
)

# Firstly,
# It is currently not possible to add additional entries
# to the /var/lib/dpkg/status file created by rules_distroless,
# so we will have to put it in some other directory.
#
# Secondly,
# Syft does not read the license from the dpkg status file,
# even though that is where bazelisk puts it,
# so we manually add a copyright file which specifies it.
pkg_tar(
    name = "bazelisk_status",
    srcs = [
        "copyright",
    ],
    package_dir = "usr/share/doc/bazelisk",
    deps = [
        ":dpkg_status",
    ],
)

pkg_tar(
    name = "bazelisk",
    srcs = [],
    compressor = "//tools/zstd",
    compressor_args = "--ultra -22",
    extension = "tar.zst",
    deps = [
        ":bazelisk_status",
        ":data",
    ],
)
